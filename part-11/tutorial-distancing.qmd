---
title: "Tutorial XI - Arena Seat Planning under Distancing Rules"
subtitle: "Applied Optimization with Julia"

format:
    html:
        theme: [litera, ../styles.scss]
        highlight-style: arrow
        linkcolor: "#a60000"
        code-copy: true
        code-link: true
        toc: true
        toc-location: right
        code-overflow: wrap
    ipynb:
        code-copy: true
        code-overflow: wrap
---

# Introduction

Remember the `Example by hand` from the lecture? In this tutorial, we will solve the arena seating problem **by computer**. As we approach the end of the course, we'll remove some previous "guardrails" to give you more freedom in solving the problem.

:::{.callout-tip}

Don't worry, if you cannot solve everything by yourself. Try your best and ask for help if you need it!

:::

# 1. Implement the Model

First, define all necessary sets, parameters, and variables to model the problem in Julia. The seating area layout is shown below:

![](https://images.byndsim.com/ao/ao_arena-empty_exercise.svg)

## Group Types Data

The values of the groups are given in the julia code below.

::: {.callout-note}
The groups are given **differently than in the lecture**! Either adjust the data or the model, depending on what you think is easier.
:::

## Distance Requirements

The following distancing rules must be maintained:

- Minimum one empty seat between groups
- One empty seat between rows
- One empty seat diagonally
- Maximum two groups per row
- Grey seats are obstacles and cannot be used

## Define the Model

::: {.content-visible when-profile="solutions"}
```{julia}
using JuMP
using HiGHS

# Model
arena_model = Model(HiGHS.Optimizer)

# Sets
row_set = 1:10
col_set = 1:10

# Group data
groups = ["a", "b", "c", "d", "e", "f", "g"]
req_seats = Dict("a" => 1, "b" => 2, "c" => 2, "d" => 4, "e" => 4, "f" => 6, "g" => 6)
scores = Dict("a" => 1, "b" => 2, "c" => 4, "d" => 4, "e" => 5, "f" => 6, "g" => 12)
availability = Dict("a" => 3, "b" => 2, "c" => 3, "d" => 5, "e" => 2, "f" => 1, "g" => 1)

# Variables
@variable(arena_model, x[groups, row_set, col_set], Bin)

# Parameters
h = 1  # horizontal safety distance
b = 1  # vertical safety distance
p = 2  # max groups per row

# Objective
@objective(arena_model, Max, sum(scores[g] * x[g,r,c] for g in groups, r in row_set, c in col_set if c<=maximum(col_set)-req_seats[g]+1))

# Constraints
# Each group can only be assigned once
@constraint(arena_model, [g in groups], 
    sum(x[g,r,c] for r in row_set, c in col_set) <= availability[g])

# Maximum groups per row
@constraint(arena_model, [r in row_set],
    sum(x[g,r,c] for g in groups, c in col_set) <= p)

# Horizontal and vertical social distancing
@constraint(arena_model, [r in row_set, c in col_set],
    sum(x[g,rr,cc] for g in groups, 
        rr in max(1,r-b):r, 
        cc in max(1,c-req_seats[g]+1-h):c) <= 1)

# Define blocked seats (coordinates [row, column])
blocked_seats = [
    (1, 1),(1, 2),(1,9),(1,10), 
    (6, 5),(6,6), 
    (7, 5),(7,6), 
]

# Constraints to prevent assignments to blocked seats
@constraint(arena_model, [g in groups, (r,c) in blocked_seats], 
    x[g,r,c] == 0)

# Solve the model
optimize!(arena_model)
```
:::

::: {.content-visible unless-profile="solutions"}

```{julia}
#| output: false
#| eval: false
using JuMP
using HiGHS

# Model
arena_model = Model(HiGHS.Optimizer)

# Sets
row_set = 1:10
col_set = 1:10

# Group data
groups = ["a", "b", "c", "d", "e", "f", "g"]
req_seats = Dict("a" => 1, "b" => 2, "c" => 2, "d" => 4, "e" => 4, "f" => 6, "g" => 6)
scores = Dict("a" => 1, "b" => 2, "c" => 4, "d" => 4, "e" => 5, "f" => 6, "g" => 12)
availability = Dict("a" => 3, "b" => 2, "c" => 3, "d" => 5, "e" => 2, "f" => 1, "g" => 1)

# Variables
@variable(arena_model, x[groups, row_set, col_set], Bin)

# YOUR CODE BELOW

# Suggested structure:
# 1. Create parameters
# 2. Set objective function
# 3. Add constraints
# 4. Solve the model

```
:::

## Visualization

To test your solution, visualize it with a plot in Julia. The visualization is a great tool to [check if your solution is correct]{.highlight}. 

We figure it is likely, that you **won't have an applicable solution** after the first round, even if your model is working correctly. [If everything works from the start, great!]{.highlight}

```{julia}
#| eval: false
using Plots

# Create visualization of the solution
function visualize_seating(model)
    # Get solution values
    solution_matrix = fill("", 10, 10)
    
    # Fill matrix with group assignments
    for r in 1:10, c in 1:10
        for g in groups
            if value(model[:x][g,r,c]) > 0.5  # Using 0.5 to handle floating point
                solution_matrix[r,c] = g
            end
        end
    end
    
    # Create color mapping for groups
    color_map = Dict(
        "" => :white,  # Empty seats
        "a" => :blue,
        "b" => :green,
        "c" => :red,
        "d" => :purple,
        "e" => :orange,
        "f" => :yellow,
        "g" => :pink
    )
    
    # Mark blocked seats
    for (r,c) in blocked_seats
        solution_matrix[r,c] = ""  # Empty string for blocked seats
    end
    
    # Create plot
    p = plot(
        aspect_ratio=:equal,
        xlims=(0.5,10.5),
        ylims=(0.5,10.5),
        yflip=true,  # Flip y-axis to match traditional seating layout
        legend=:outerright
    )
    
    # Plot seats
    for r in 1:10, c in 1:10
        group = solution_matrix[r,c]
        if group != ""
            group_length = req_seats[group]
            for i in 1:group_length
                if c+i-1 <= 10
                    println("Group $group in $r,$(c+i-1)")
                    scatter!([c+i-1], [r], 
                            color=color_map[group],
                            label=nothing,
                            markersize=10,
                            markershape=:square)
                end
            end
        else
            # Plot empty or blocked seats
            is_blocked = (r,c) in blocked_seats
            if is_blocked
                println("Blocked seat in $r,$c")
                scatter!([c], [r], 
                        color=is_blocked ? :gray : :white,
                        markersize=10,
                        markershape=:square,
                        label= nothing)
            end
        end
    end
    
    title!("Arena Seating Layout")
    xlabel!("Column")
    ylabel!("Row")
    
    return p
end

# Display the visualization
p = visualize_seating(arena_model)
display(p)
```

If you encounter any difficulties ad cannot solve the problem, please document your issues here:

```{julia}
#=



=#
```

---

{{< include ../include/tutorial_end.qmd >}}